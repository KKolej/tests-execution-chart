plugins {
    id 'groovy'
    id 'java'
    id 'io.github.platan.tests-execution-chart'
}

apply from: '../checks.gradle'

repositories {
    mavenCentral()
}

// based on https://docs.gradle.org/current/userguide/java_testing.html#sec:configuring_java_integration_tests
sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    intTestImplementation 'org.codehaus.groovy:groovy-all:3.0.10'
    intTestImplementation platform("org.spockframework:spock-bom:2.1-groovy-3.0")
    intTestImplementation "org.spockframework:spock-core"
}

tasks.register('integrationTest', Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    useJUnitPlatform()
}

test.dependsOn integrationTest
check.dependsOn integrationTest

createTestsExecutionReport {
    formats {
        html {
            enabled = true
            script {
                embed = true
            }
        }
        json { enabled = true }
        mermaid { enabled = true }
    }
}

checkReports {
    jsonReport = 'build/reports/tests-execution/json/integrationTest.json'
    htmlReport = 'build/reports/tests-execution/html/integrationTest.html'
    mermaidReport = 'build/reports/tests-execution/mermaid/integrationTest.txt'
}
